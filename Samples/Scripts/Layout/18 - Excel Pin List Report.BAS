'This script has been generated by PADS Layout's VB Script Wizard on 4/4/2018 10:30:15 AM
' Sample 18: Excel Pin List Report.BAS
' 
' This sample demonstrates how to generate PADS Layout reports in Excel.
'
' For more details, please refer to the PADS Layout Basic Editor Help File.
'

'Array of column names. You can modify it to rename columns
Const Columns = Array("Name", "Component", "Net", "Drill Size", "Position X", "Position Y", "Plated", "Test Point", "Thermal", "Glued", "SMD")
Dim fname As String

Sub Main
	fname = ActiveDocument
	If fname = "" Then
		fname = "Untitled"
	End If
	tempFile = DefaultFilePath & "\temp.txt"
	Open tempFile For Output As #1

	StatusBarText = "Generating report..."
	'Output table header
	For i = 0 to UBound(Columns)
		OutCell Columns(i)
	Next
	Print #1
	'Output table rows
	For Each aPin in GetSortedPins(ActiveDocument)
		OutCell aPin.Name
		OutCell aPin.Component
		If (aPin.Net Is Nothing) Then NetName = "N/A" Else NetName = ObjName(aPin.Net)
		outcell NetName
		OutCell aPin.DrillSize
		OutCell Format(aPin.PositionX, "0.00")
		OutCell Format(aPin.PositionY, "0.00")
		OutCell aPin.Plated
		OutCell IIf (aPin.TestPoint = ppcbTestPointBottomLayer, "Bottom", IIf (aPin.TestPoint = ppcbTestPointTopLayer, "Top", "No"))
		OutCell aPin.PlaneThermal
			GluedComp = apin.Component.Glued
		OutCell Format(GluedComp, "Yes/No")
		OutCell Format(aPin.IsSMD, "True/False")
		Print #1
	Next aPin

	StatusBarText = ""
	Close #1
	ExportToExcel
End Sub

'Pins are not sorted by default (performance issue), so sort them explicitly in report
Function GetSortedPins(obj As Object)
	Set GetSortedPins = obj.Pins
	GetSortedPins.Sort
End Function

Function PinType(aPin As Object) As String
		Select Case aPin.ElectricalType
			Case ppcbElectricalTypeSource
				PinType = "Source"
			Case ppcbElectricalTypeBidirectional
				PinType = "Bidirectional"
			Case ppcbElectricalTypeOpenCollector
				PinType = "Collector"
			Case ppcbElectricalTypeOrTieableSource
				PinType = "Tieable Source"
			Case ppcbElectricalTypeTristate
				PinType = "Tristate"
			Case ppcbElectricalTypeLoad
				PinType = "Load"
			Case ppcbElectricalTypeTerminator
				PinType = "Terminator"
			Case ppcbElectricalTypePower
				PinType = "Power"
			Case ppcbElectricalTypeGround
				PinType = "Ground"
			Case Else 
				PinType = "Unknown"
		End Select	
End Function

Function ObjName (obj As Object)
	ObjName = IIf(obj Is Nothing, "", obj)
End Function

Sub ExportToExcel
	FillClipboard
	Dim xl As Object
	On Error Resume Next
	Set xl =  GetObject(,"Excel.Application")
	On Error GoTo ExcelError	' Enable error trapping.
	If xl Is Nothing Then
		Set xl =  CreateObject("Excel.Application")
	End If
	xl.Visible = True
	xl.Workbooks.Add
	xl.ActiveSheet.Paste
	xl.Range("A1:K1").Font.Bold = True
	xl.Range("A1:K1").NumberFormat = "@"
	xl.Range("A1:K1").AutoFilter
	xl.ActiveSheet.UsedRange.Columns.AutoFit
	'Output Report Header
	'xl.Rows(1).Insert
	'xl.Rows(1).Cells(1) = Space(1) & "Pin Report for " & fname & " on " & Now
	'xl.Rows(2).Insert
	xl.Rows(1).Font.bold = True
	xl.Range("A1").Select
	On Error GoTo 0 ' Disable error trapping. 
	Exit Sub    

ExcelError:
	MsgBox Err.Description, vbExclamation, "Error Running Excel"
	On Error GoTo 0 ' Disable error trapping.    
	Exit Sub
End Sub

Sub OutCell (txt As String)
	Print #1, txt; vbTab;
End Sub

Sub FillClipboard
	StatusBarText = "Export Data To Clipboard..."
	' Load whole file to string variable    
	tempFile = DefaultFilePath & "\temp.txt"
	Open tempFile  For Input As #1
	L = LOF(1)
	AllData$ = Input$(L,1)
	Close #1
	'Copy whole data to clipboard
	Clipboard AllData$ 
	Kill tempFile
	StatusBarText = ""
End Sub
